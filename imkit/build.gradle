apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}
publishing {
    publications {
        maven(MavenPublication) {
            artifact sourceJar
            artifact bundleRelease
            //The publication doesn't know about our dependencies, so we have to manually add them to the pom

            pom.withXml {
                // for dependencies and exclusions

                def dependencies = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    if (it.group?.length() > 0) {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        // for exclusions

                        if (it.getExcludeRules()?.size() > 0) {
                            def exclusions = dependency.appendNode('exclusions')
                            it.getExcludeRules()?.each {
                                def exclusion = exclusions.appendNode('exclusion')
                                exclusion.appendNode('groupId', it.group)
                                exclusion.appendNode('artifactId', it.module)
                            }
                        }
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile project(':imsdk')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.7.1'
    compile 'com.netflix.rxjava:rxjava-core:0.17.6'
    compile 'com.netflix.rxjava:rxjava-android:0.17.6'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile('org.apache.commons:commons-io:1.3.2') {
        exclude group: 'commons-io', module: 'commons-io'
        //added this because crashlytics seems to be using commons-io
    }
    compile 'joda-time:joda-time:2.5'
    compile 'com.commit451:PhotoView:1.2.5'
    compile 'com.amap.api:location:latest.integration'
    compile 'com.amap.api:map2d:latest.integration'
    compile 'com.amap.api:search:latest.integration'
    testCompile 'junit:junit:4.12'
    testCompile 'net.sf.json-lib:json-lib:2.4:jdk15'
    testCompile 'xom:xom:1.2.5'
    testCompile 'org.json:json:20160810'
}
